#!/bin/tcsh

# 一時ファイル名
    set tmp="/tmp/exif_output.txt"

# ファイルのフルパスを取得
    set filepath = `realpath "$1"`

# exiftoolで出力を一時ファイルに保存
    exiftool -d "%Y%m%d %H%M%S" -DateTimeOriginal -ImageSize -Model -T "$1" > ${tmp}

# 出力結果を行ごとに処理する
    set datetime  = `cat ${tmp} | cut -f 1`
    set imagesize = `cat ${tmp} | cut -f 2`
    set model     = `cat ${tmp} | cut -f 3`

# 撮影日と時刻の処理
    set date_part = `echo $datetime | cut -d " " -f 1`
    set time_part = `echo $datetime | cut -d " " -f 2`

    if ("$date_part" == "-" || "$time_part" == "-") then
        set capture_date = "撮影日:不明"
    else
        set year    = `echo "$date_part" | cut -c 1-4`
        set month   = `echo "$date_part" | cut -c 5-6`
        set day     = `echo "$date_part" | cut -c 7-8`
        set weekday = `date -j -f "%Y%m%d" "${year}${month}${day}" "+%a"`

        set hour   = `echo "$time_part" | cut -c 1-2`
        set minute = `echo "$time_part" | cut -c 3-4`
        set second = `echo "$time_part" | cut -c 5-6`

        set capture_date = "撮影日:${year}年${month}月${day}日(${weekday}) ${hour}時${minute}分${second}秒"
    endif

    # 画像の解像度を取得
    if ("$imagesize" == "-") then
        set resolution = "サイズ:不明"
    else
        set resolution = "サイズ:$imagesize"
    endif

    # デジカメの機種名を取得
    if ("$model" == "-") then
        set camera_model = "機種名:不明"
    else
        set camera_model = "機種名:$model"
    endif

    # 結果を標準出力に表示
    echo "$filepath　"
    echo "$capture_date　"
    echo "$resolution　"
    echo "$camera_model"

# 一時ファイルを削除
    \rm ${tmp}
